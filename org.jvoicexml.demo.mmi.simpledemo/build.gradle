apply plugin: 'java'
apply plugin: 'application'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.3'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.20.v20190813'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.11'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.9'
    if (project.property('JVOICEXML_DEVELOPMENT_ENVIRONMENT') == 'true') {
        compile project(':org.jvoicexml.client')
        compile project(':org.jvoicexml.xml')
        compile project(':org.jvoicexml.mmi.events')

        runtime project(':org.jvoicexml.callmanager.mmi')
        runtime project(':org.jvoicexml.callmanager.mmi.http')
        runtime project(':org.jvoicexml.jndi')
    } else {
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.client', version: JVOICEXML_VERSION
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.xml', version: JVOICEXML_VERSION
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.mmi.events', version: JVOICEXML_VERSION
    
        runtime group: 'org.jvoicexml', name: 'org.jvoicexml.callmanager.mmi', version: JVOICEXML_VERSION
        runtime group: 'org.jvoicexml', name: 'org.jvoicexml.callmanager.mmi.http', version: JVOICEXML_VERSION
        runtime group: 'org.jvoicexml', name: 'org.jvoicexml.jndi', version: JVOICEXML_VERSION
    }
}

jar {
    version = JVOICEXML_VERSION

    manifest {
        attributes("Implementation-Title": "JVoiceXML Simple MMI Demo",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.demo.mmi.simpledemo'
}

run {
    mainClassName = "org.jvoicexml.demo.mmi.simpledemo.SimpleMmiDemo"
    classpath += files("${buildDir}/resources/main")
    jvmArgs = ["-Djvoicexml.config=${buildDir}/resources/main",
               "-Djava.security.manager",
               "-Djava.security.policy=${projectDir}/src/main/config/jvoicexml.policy"]
}
