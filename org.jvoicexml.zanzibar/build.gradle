apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    compile project(':org.jvoicexml')

    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.9.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.9.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2.9.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.9.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.9.1'
    compile group: 'org.asteriskjava', name: 'asterisk-java', version: '0.3'
    compile group: 'net.sourceforge.halef', name: 'cairo-sip', version: '0.3-SNAPSHOT'
    compile group: 'net.sourceforge.halef', name: 'cairo-rtp', version: '0.3-SNAPSHOT'
    compile group: 'net.sourceforge.halef', name: 'cairo-client', version: '0.3-SNAPSHOT'
    compile group: 'net.sourceforge.halef', name: 'cairo-server', version: '0.3-SNAPSHOT'
    compile group: 'net.sourceforge.halef', name: 'halef-mrcp4j', version: '0.1-SNAPSHOT'
    compile group: 'commons-configuration', name: 'commons-configuration', version: '1.6'
    compile group: 'org.springframework', name: 'spring', version: '2.0'
    compile fileTree(dir: './3rdparty/jsapi1.0/lib', include: ['*.jar'])

    testCompile 'junit:junit:4.12'
}

jar {
    version = JVOICEXML_VERSION
    manifest {
        attributes("Implementation-Title": "JVoiceXML Port of Zanzibar",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.xml'
}

install {
   version = JVOICEXML_VERSION
   group = JVOICEXML_GROUP
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    version = JVOICEXML_VERSION
    group = JVOICEXML_GROUP

    repositories {
        mavenDeployer {
            beforeDeployment { 
                MavenDeployment deployment -> signing.signPom(deployment) 
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: JVOICEXML_OSSRH_USERNAME, password: JVOICEXML_OSSRH_PASSWORD)
            }
    
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: JVOICEXML_OSSRH_USERNAME, password: JVOICEXML_OSSRH_PASSWORD)
            }
    
            pom.project {
                name 'JVoiceXML'
                groupId = JVOICEXML_GROUP
                packaging 'jar'
                description 'The Open Source Voice Browser'
                url 'https://github.com/JVoiceXML/JVoiceXML'
                
                scm {
                    connection 'scm:git:git@github.com:JVoiceXML/JVoiceXML.git'
                    developerConnection 'scm:git:ssh://github.com:JVoiceXML/JVoiceXML.git'
                    url "https://github.com/JVoiceXML/JVoiceXML/tree/release/${JVOICEXML_VERSION}"
                }
                    
                licenses {
                    license {
                        name 'GNU Lesser General Public License, Version 2.1'
                        url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
    
                developers {
                    developer {
                        id 'schnelle'
                        name 'Dirk Schnelle-Walka'
                        email 'dirk.schnelle@jvoicexml.org'
                    }
                }
            }
        }
    }
}
