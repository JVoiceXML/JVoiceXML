buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'com.github.eerohele.saxon-gradle' version '0.8.0'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = JVOICEXML_GROUP
version = JVOICEXML_VERSION

configurations {
    jvxmlConfiguration
}

def jvxmlConfigurationDir = "${projectDir}/src/main/config"

dependencies {
    jvxmlConfiguration fileTree(jvxmlConfigurationDir)

    compile group: 'org.mozilla', name: 'rhino', version: '1.7.10'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'

    compile project(':org.jvoicexml')
    
    testCompile group: 'junit', name: 'junit', version: '4.13.2'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile project(path: ':org.jvoicexml', configuration: 'testArtifacts')
}

xslt {
    stylesheet "${jvxmlConfigurationDir}/patch-configuration.xsl"
    input "${jvxmlConfigurationDir}/ecmascript-datamodel.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/ecmascript-datamodel.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task xsltDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${jvxmlConfigurationDir}/patch-configuration.xsl"
    input "${jvxmlConfigurationDir}/ecmascript-datamodel.xml"
    output "${project(':org.jvoicexml').copyConfigurationDistribution.destinationDir}/ecmascript-datamodel.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include 'rhino*.jar',
         'json-simple*.jar'
    }
}

jar {
    version = JVOICEXML_VERSION

    dependsOn xslt
    dependsOn xsltDistribution
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML ECMAScript data model",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.interpreter.datamodel.ecmascript'
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/ecmascript-datamodel.xml")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = tasks.jar.baseName
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'JVoiceXML ECMAScript data model'
                description = 'JVoiceXML ECMAScript data model'
                url = 'https://github.com/JVoiceXML/JVoiceXML'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, Version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
                developers {
                    developer {
                        id = 'schnelle'
                        name = 'Dirk Schnelle-Walka'
                        email = 'dirk.schnelle@jvoicexml.org'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:JVoiceXML/JVoiceXML.git'
                    developerConnection = 'scm:git:ssh://github.com:JVoiceXML/JVoiceXML.git'
                    url = 'https://github.com/JVoiceXML/JVoiceXML'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/" 
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = JVOICEXML_OSSRH_USERNAME
                password = JVOICEXML_OSSRH_PASSWORD
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

