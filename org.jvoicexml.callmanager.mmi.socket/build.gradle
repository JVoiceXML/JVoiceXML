apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    compile group: 'org.mozilla', name: 'rhino', version: '1.7R4'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
    compile project(':org.jvoicexml.mmi.events')
    compile project(':org.jvoicexml.profile.mmi')
    compile project(':org.jvoicexml.callmanager.mmi')
}

xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/mmi-callmanager-socket.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/mmi-callmanager-socket.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: version,
        libs: configurations.compile
    )
}

task xsltDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/mmi-callmanager-socket.xml"
    output "${project(':main').buildDir}/config/mmi-callmanager-socket.xml"
    parameters(
        buildpath: "lib",
        version: version,
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include 'rhino*.jar',
        'json-simple*.jar',
        'org.jvoicexml.mmi.events*.jar',
        'org.jvoicexml.mmi.profile.mmi*.jar',
        'org.jvoicexml.callmanager.mmi*.jar'
    }
}

jar {
    dependsOn xslt
    dependsOn xsltDistribution
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML Socket ETL of the MMI call manager",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": version)
    }
    baseName 'org.jvoicexml.callmanager.mmi.socket'
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/mmi-callmanager-socket.xml")
}
