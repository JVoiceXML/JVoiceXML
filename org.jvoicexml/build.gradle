apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

configurations {
    jvxmlConfiguration
    testArtifacts
}

dependencies {
    jvxmlConfiguration fileTree(dir: "src/main/config")
    testArtifacts sourceSets.test.output
    
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.9.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.9.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2.9.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.9.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.9.1'
    compile group: 'org.mozilla', name: 'rhino', version: '1.7R4'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.14.v20161028'
    compile group: 'net.sourceforge.jchardet', name: 'jchardet', version: '1.0'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.5'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
    compile group: 'commons-pool', name: 'commons-pool', version: '1.6'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml.xml')
}

task copyConfiguration(type: Copy) {
    from configurations.jvxmlConfiguration.files
    into "$buildDir/config"
}

task copyConfigurationDistribution(type: Copy) {
    from configurations.jvxmlConfiguration.files
    into "${project(':main').buildDir}/config"
}

task createVersionFile() << {
    def file = new File("${sourceSets.main.output.resourcesDir}/jvoicexml.version")
    file.createNewFile()
    file.text = "jvxml.version=${JVOICEXML_VERSION}"
}

jar {
    version = JVOICEXML_VERSION

    dependsOn createVersionFile
    dependsOn copyConfiguration
    dependsOn copyConfigurationDistribution

    manifest {
        attributes("Implementation-Title": "JVoiceXML core library",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": JVOICEXML_VERSION)
    }
    baseName 'org.jvoicexml'
}

install {
   version = JVOICEXML_VERSION
   group = JVOICEXML_GROUP
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { 
                MavenDeployment deployment -> signing.signPom(deployment) 
            }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: JVOICEXML_OSSRH_USERNAME, password: JVOICEXML_OSSRH_PASSWORD)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: JVOICEXML_OSSRH_USERNAME, password: JVOICEXML_OSSRH_PASSWORD)
        }

        pom.project {
            groupId = "org.jvoicexml"
            licenses {
            license {
                name 'GNU Lesser General Public License, Version 2.1'
                url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
            }
        }

        developers {
            developer {
                id 'dirk'
                name 'Dirk Schnelle-Walka'
                email 'dirk.schnelle@jvoicexml.org'
            }
        }
      }
    }
  }
}
