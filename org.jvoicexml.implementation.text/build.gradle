apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenCentral()
}


sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.1.0'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
    compile project(':org.jvoicexml.client.text')
    compile project(':org.jvoicexml.interpreter.grammar.luis')
    compile project(':org.jvoicexml.srgs')
}

xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/text-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/text-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        subscriptionId: project.property('JVOICEXML_INTERPRETER_GRAMMAR_LUIS_SUBSCRIPTIONID'),
        libs: configurations.compile
    )
}

task xsltDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/text-implementation.xml"
    output "${project(':main').buildDir}/config/text-implementation.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        subscriptionId: project.property('JVOICEXML_INTERPRETER_GRAMMAR_LUIS_SUBSCRIPTIONID'),
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include 'protobuf-java*.jar',
        'json-simple*.jar',
        'org.jvoicexml.client.text*.jar',
        'org.jvoicexml.srgs*.jar',
        'org.jvoicexml.interpreter.grammar.luis*.jar'
    }
}

jar {
    version = JVOICEXML_VERSION

    dependsOn xslt
    dependsOn xsltDistribution
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML text implementation platform",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.implementation.text'
}

install {
   version = JVOICEXML_VERSION
   group = JVOICEXML_GROUP
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/text-implementation.xml")
}
