apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

configurations.all {
    exclude group: 'com.spokentech', module:'speechcloud-client'
    exclude group: 'log4j', module: 'log4j'
}

dependencies {
    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
    compile project(':org.jvoicexml.client.mrcpv2')
    compile project(':org.jvoicexml.interpreter.grammar.halef')
    compile project(':org.jvoicexml.zanzibar')
    
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2.11.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.11.2'
    compile group: 'org.jvoicexml', name: 'org.speechforge.cairo.client', version: '0.3.1-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'org.speechforge.cairo.sip', version: '0.3.1-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'org.speechforge.cairo.rtp', version: '0.3.1-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'org.mrcp4j', version: '0.3-SNAPSHOT'
    compile group: 'concurrent', name: 'concurrent', version: '1.3.4'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'javax.media', name: 'jmf', version: '2.1.1e'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

task xsltImplementation(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/mrcpv2-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/mrcpv2-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task xsltImplementationDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/mrcpv2-implementation.xml"
    output "${project(':main').buildDir}/config/mrcpv2-implementation.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

def getIP() {
    InetAddress result = null;
    Enumeration<NetworkInterface> interfaces = NetworkInterface.getNetworkInterfaces();
    while (interfaces.hasMoreElements()) {
        Enumeration<InetAddress> addresses = interfaces.nextElement().getInetAddresses();
        while (addresses.hasMoreElements()) {
            InetAddress address = addresses.nextElement();
            if (!address.isLoopbackAddress()) {
                if (address.isSiteLocalAddress()) {
                    return address.getHostAddress();
                } else if (result == null) {
                    result = address;
                }
            }
        }
    }
    return (result != null ? result : InetAddress.getLocalHost()).getHostAddress();
}

task xsltCallmanager(type: com.github.eerohele.SaxonXsltTask)  {
    stylesheet "${projectDir}/src/main/resources/patch-callmanager-configuration.xsl"
    input "${projectDir}/src/main/resources/mrcpv2-callmanager.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/mrcpv2-callmanager.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compile,
        address: JVOICEXML_IMPLEMENTATION_MRCPV2_SIP_ADDRESS,
        port: JVOICEXML_IMPLEMENTATION_MRCPV2_SIP_PORT,
        cairoAddress: "sip:cairo@${getIP()}",
        cairoHost: getIP(),
        cairoPort: JVOICEXML_IMPLEMENTATION_MRCPV2_CAIRO_PORT,
        baseReceiverRTPPort: JVOICEXML_IMPLEMENTATION_MRCPV2_CAIRO_BASE_RCV_RTP_PORT,
        baseTransmitterRTPPort: JVOICEXML_IMPLEMENTATION_MRCPV2_CAIRO_BASE_XMIT_RTP_PORT
    )
}

task xsltCallmanagerDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-callmanager-configuration.xsl"
    input "${projectDir}/src/main/resources/mrcpv2-callmanager.xml"
    output "${project(':main').buildDir}/config/mrcpv2-callmanager.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compile,
        address: JVOICEXML_IMPLEMENTATION_MRCPV2_SIP_ADDRESS,
        port: JVOICEXML_IMPLEMENTATION_MRCPV2_SIP_PORT,
        cairoAddress: JVOICEXML_IMPLEMENTATION_MRCPV2_CAIRO_ADDRESS,
        cairoHost: JVOICEXML_IMPLEMENTATION_MRCPV2_CAIRO_HOST,
        cairoPort: JVOICEXML_IMPLEMENTATION_MRCPV2_CAIRO_PORT,
        baseReceiverRTPPort: JVOICEXML_IMPLEMENTATION_MRCPV2_CAIRO_BASE_RCV_RTP_PORT,
        baseTransmitterRTPPort: JVOICEXML_IMPLEMENTATION_MRCPV2_CAIRO_BASE_XMIT_RTP_PORT
    )
}

task copyReferencedLibraries(type: Copy) {
    into "${buildDir}/libs"
    from(configurations.compile) {
        include 'org.speechforge.cairo.client*.jar',
         'org.speechforge.cairo.sip*.jar',
         'org.speechforge.cairo.rtp*.jar',
         'org.mrcp4j*.jar',
         'org.jvoicexml.client.mrcpv2*.jar',
         'org.jvoicexml.zanzibar*.jar',
         'org.jvoicexml.interpreter.grammar.halef*.jar',
         'jain-sip-api*.jar',
         'jain-sip-ri*.jar',
         'nist-sdp*.jar',
         'concurrent*.jar',
         'commons-lang*.jar',
         'jmf*.jar'
    }
}

jar {
    version = JVOICEXML_VERSION

    dependsOn xsltImplementation
    dependsOn xsltImplementationDistribution
    dependsOn xsltCallmanager
    dependsOn xsltCallmanagerDistribution
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML MRCPv2 implementation platform",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.implementation.mrcpv2'
}

install {
   version = JVOICEXML_VERSION
   group = JVOICEXML_GROUP
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/mrcpv2-callmanager.xml")
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/mrcpv2-implementation.xml")
}
