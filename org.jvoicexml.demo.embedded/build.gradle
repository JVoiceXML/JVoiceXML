plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
}

repositories {
    mavenCentral()
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle {
    configFile file("${project(':org.jvoicexml').projectDir}/src/templates/jvoicexml-checks.xml")
}

dependencies {
    if (project.property('JVOICEXML_DEVELOPMENT_ENVIRONMENT') == 'true') {
        compile project(':org.jvoicexml')
        compile project(':org.jvoicexml.client')
        compile project(':org.jvoicexml.client.text')
        compile project(':org.jvoicexml.profile.vxml21')
        compile project(':org.jvoicexml.implementation.text')
        compile project(':org.jvoicexml.interpreter.datamodel.ecmascript')
        compile project(':org.jvoicexml.documentserver.schemestrategy.file')
        compile project(':org.jvoicexml.documentserver.schemestrategy.http')
        compile project(':org.jvoicexml.srgs')
    } else {
        compile group: 'org.jvoicexml', name: 'org.jvoicexml', version: JVOICEXML_VERSION
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.client', version: JVOICEXML_VERSION
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.client.text', version: JVOICEXML_VERSION
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.profile.vxml21', version: JVOICEXML_VERSION
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.implementation.text', version: JVOICEXML_VERSION
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.interpreter.datamodel.ecmascript', version: JVOICEXML_VERSION
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.documentserver.schemestrategy.file', version: JVOICEXML_VERSION
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.documentserver.schemestrategy.http', version: JVOICEXML_VERSION
        compile group: 'org.jvoicexml', name: 'org.jvoicexml.srgs', version: JVOICEXML_VERSION
    }
}

jar {
    version = JVOICEXML_VERSION

    manifest {
        attributes("Implementation-Title": "JVoiceXML Embbeded Demo",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.demo.embedded'
}

mainClassName = "org.jvoicexml.demo.embedded.EmbeddedJVoiceXML"

run {
    classpath = project.sourceSets.test.runtimeClasspath + project(':org.jvoicexml').sourceSets.main.resources
    jvmArgs = ["-Djvoicexml.config=${project(':org.jvoicexml').projectDir}/config",
                             "-Djava.security.policy=${project(':org.jvoicexml').projectDir}/config/jvoicexml.policy" ]
}

