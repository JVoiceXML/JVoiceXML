buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'com.github.eerohele.saxon-gradle' version '0.8.0'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
   }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = JVOICEXML_GROUP
version = JVOICEXML_VERSION

configurations {
    jvxmlConfiguration
}

def jvxmlConfigurationDir = "${projectDir}/src/main/config"

dependencies {
    jvxmlConfiguration fileTree(jvxmlConfigurationDir)

    implementation group: 'org.jvoicexml', name: 'javax.speech', version: '0.6-SNAPSHOT'
    implementation group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2', version: '0.6-SNAPSHOT'
    implementation group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2', version: '0.6-SNAPSHOT'
    implementation group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2.freetts', version: '0.6-SNAPSHOT'
    implementation group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2.sphinx4', version: '0.6-SNAPSHOT'
    implementation group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2.sapi', version: '0.6-SNAPSHOT'
    implementation group: 'org.jvoicexml', name: 'cmu_us_kal', version: '1.2.3-SNAPSHOT'
    
    
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testImplementation testFixtures(project(':org.jvoicexml'))

    implementation project(':org.jvoicexml')
    implementation project(':org.jvoicexml.client')
}

task xsltSphinxFreeTTS(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${jvxmlConfigurationDir}/patch-configuration.xsl"
    input "${jvxmlConfigurationDir}/jsapi20-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi20-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compileClasspath
    )
}

task xsltSphinxFreeTTSDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${jvxmlConfigurationDir}/patch-configuration.xsl"
    input "${jvxmlConfigurationDir}/jsapi20-implementation.xml"
    output "${project(':org.jvoicexml').copyConfigurationDistribution.destinationDir}/jsapi20-implementation.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compileClasspath
    )
}

task xsltSAPI(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${jvxmlConfigurationDir}/patch-configuration.xsl"
    input "${jvxmlConfigurationDir}/jsapi20-sapi-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi20-sapi-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compileClasspath
    )
}

task xsltSAPIDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${jvxmlConfigurationDir}/patch-configuration.xsl"
    input "${jvxmlConfigurationDir}/jsapi20-sapi-implementation.xml"
    output "${project(':org.jvoicexml').copyConfigurationDistribution.destinationDir}/jsapi20-sapi-implementation.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compileClasspath
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.runtimeClasspath) {
         include 'freetts*.jar',
         'cmu_us_kal*.jar',
         'cmulex*.jar',
         'cmutimelex*.jar',
         'en_us*.jar',
         'sphinx4*.jar',
         'javax.speech*.jar',
         'org.jvoicexml.jsapi2*.jar'
    }
}

task copyJNI(type: Copy) {
    dependsOn copyReferencedLibraries
    
    from(zipTree("$buildDir/libs/org.jvoicexml.jsapi2.sapi-0.6-SNAPSHOT.jar")) {
         include '*.dll'
    }
    into "${project(':org.jvoicexml').buildDir}/jni"
}

jar {
    version = JVOICEXML_VERSION

    dependsOn xsltSphinxFreeTTS {
        onlyIf { 
            project.property('JVOICEXML_IMPLEMENTATION_JSAPI20_FREETTSSPHINX') == 'true'
        }
    }
    dependsOn xsltSphinxFreeTTSDistribution
    dependsOn xsltSAPI {
        onlyIf { 
            project.property('JVOICEXML_IMPLEMENTATION_JSAPI20_SAPI') == 'true'
        }
    }
    dependsOn xsltSAPIDistribution
    
    dependsOn copyJNI
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML JSAPI 2.0 implementation platform",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.implementation.jsapi20'
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi20-implementation.xml")
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi20-sapi-implementation.xml")
    delete file("${project(':org.jvoicexml').buildDir}/jni/Jsapi2SapiBridge.dll")
    delete file("${project(':org.jvoicexml').buildDir}/jni/Jsapi2SapiBridge_x64.dll")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = tasks.jar.baseName
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'JVoiceXML JSAPI 2.0 implementation platform'
                description = 'JVoiceXML JSAPI 2.0 implementation platform'
                url = 'https://github.com/JVoiceXML/JVoiceXML'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, Version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
                developers {
                    developer {
                        id = 'schnelle'
                        name = 'Dirk Schnelle-Walka'
                        email = 'dirk.schnelle@jvoicexml.org'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:JVoiceXML/JVoiceXML.git'
                    developerConnection = 'scm:git:ssh://github.com:JVoiceXML/JVoiceXML.git'
                    url = 'https://github.com/JVoiceXML/JVoiceXML'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/" 
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = JVOICEXML_OSSRH_USERNAME
                password = JVOICEXML_OSSRH_PASSWORD
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

