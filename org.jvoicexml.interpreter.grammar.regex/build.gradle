apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
}


xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/regex-grammar.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/regex-grammar.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task xsltDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/regex-grammar.xml"
    output "${project(':main').buildDir}/config/regex-grammar.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

jar {
    version = JVOICEXML_VERSION

    dependsOn xslt
    dependsOn xsltDistribution

    from ('./src/main/resources') {
        include 'services/*'
        into 'META-INF'
    }

    manifest {
        attributes("Implementation-Title": "JVoiceXML Regex Grammar support",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": JVOICEXML_VERSION)
    }
    baseName 'org.jvoicexml.interpreter.grammar.regex'
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/regex-grammar.xml")
}
