apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

configurations.all {
    exclude group: 'log4j', module: 'log4j'
}

dependencies {
    compile group: 'de.dfki.mary', name: 'marytts-client', version: '5.2'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile project(path: ':org.jvoicexml', configuration: 'testArtifacts')

    compile project(':org.jvoicexml')
}

xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/mary-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/mary-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task xsltDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/mary-implementation.xml"
    output "${project(':main').buildDir}/config/mary-implementation.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include'marytts-client*.jar',
        'marytts-common*.jar',
        'marytts-signalproc*.jar'
    }
}

jar {
    version = JVOICEXML_VERSION

    dependsOn xslt
    dependsOn xsltDistribution
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML mary implementation platform",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.implementation.mary'
}

install {
   version = JVOICEXML_VERSION
   group = JVOICEXML_GROUP
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/mary-implementation.xml")
}
