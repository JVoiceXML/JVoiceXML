buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:4.0.0'
        classpath 'gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1'
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'de.undercouch.download'
apply plugin: "com.github.eerohele.saxon-gradle"

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'servlets.com', name: 'cos', version: '05Nov2002'
    compile group: 'xerces', name: 'xercesImpl', version: '2.8.0'

    providedCompile fileTree(dir: './WebContent/WEB-INF/lib', include: ['*.jar'])
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

}

task downloadTestsuite(type: Download) {
    src 'http://www.voicexml.org/wp-content/uploads/sites/2/static/vxml_test_suite_2.1_Rel_1.01.tar_.gz'
    dest "${buildDir}/testsuite/vxml_test_suite_2.1.tar.gz"
    overwrite false
}

task untarTestsuite(type: Copy) {
    dependsOn downloadTestsuite

    from tarTree("${buildDir}/testsuite/vxml_test_suite_2.1.tar.gz")
    into "${buildDir}/testsuite/" 
}

task copyVendorProperties(type: Copy) {
    dependsOn untarTestsuite

    from "${projectDir}/src/main/resources/vendor.properties"
    into"${buildDir}/testsuite/vxml_test_suite_2.1_Rel_1.01"
}

task copyBuildGradleVxml21(type: Copy) {
    dependsOn copyVendorProperties

    from "${projectDir}/src/main/resources/build.gradle"
    into"${buildDir}/testsuite/vxml_test_suite_2.1_Rel_1.01/vxml21_0.0.5"
}

task patchVxml21BuildXml(type: com.github.eerohele.SaxonXsltTask) {
    dependsOn copyBuildGradleVxml21

    stylesheet "${projectDir}/src/main/resources/patch-build-xml.xsl"
    input "${buildDir}/testsuite/vxml_test_suite_2.1_Rel_1.01/vxml21_0.0.5/build.xml"
    output "${buildDir}/testsuite/vxml_test_suite_2.1_Rel_1.01/vxml21_0.0.5/patched-build.xml"
    parameters(
        irtestdir: "${buildDir}/testsuite/vxml_test_suite_2.1_Rel_1.01/vxml21_0.0.5",
    )
}

task buildGradleVxml21(type: GradleBuild) {
    dependsOn patchVxml21BuildXml
    
    dir = "${buildDir}/testsuite/vxml_test_suite_2.1_Rel_1.01/vxml21_0.0.5/"
    buildFile = "${buildDir}/testsuite/vxml_test_suite_2.1_Rel_1.01/vxml21_0.0.5/build.gradle"
    tasks = ['dist']
}

task copyTestsuite(type: Copy) {
    dependsOn buildGradleVxml21
    
    from ("${buildDir}/testsuite/vxml_test_suite_2.1_Rel_1.01") {
        exclude 'WEB-INF/web.xml'
    }
    into "${projectDir}/WebContent/"
}

xslt {
    dependsOn untarTestsuite

    stylesheet "${projectDir}/src/main/resources/patch-web-xml.xsl"
    input "${buildDir}/testsuite/vxml_test_suite_2.1_Rel_1.01/WEB-INF/web.xml"
    output "${projectDir}/WebContent/WEB-INF/web.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        subscriptionId: project.property('JVOICEXML_INTERPRETER_GRAMMAR_LUIS_SUBSCRIPTIONID'),
        libs: configurations.compile
    )
}

war {
    dependsOn copyTestsuite
    dependsOn xslt
    
    from 'WebContent' 
    baseName 'org.jvoicexml.systemtest.servlet'
    version = JVOICEXML_VERSION

    manifest {
        attributes("Implementation-Title": "JVoiceXML Systemtest Servlet",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
}