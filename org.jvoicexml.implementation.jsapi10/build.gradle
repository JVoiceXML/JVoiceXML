apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenCentral()
}


sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    //compile fileTree(dir: "${project(':org.jvoicexml').projectDir}/3rdparty/freetts1.2/lib", include: ['*.jar'])
    //compile fileTree(dir: './3rdparty/freetts1.2/lib', include: ['*.jar'])
    //compile fileTree(dir: './3rdparty/jsapi1.0/lib', include: ['*.jar'])
    //compile fileTree(dir: './3rdparty/sphinx4/lib', include: ['*.jar'])
    compile fileTree(dir: "./3rdparty/talkingjava/lib", include: ['*.jar'])
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile project(path: ':org.jvoicexml', configuration: 'testArtifacts')
    //testCompile fileTree(dir: "${project(':org.jvoicexml').projectDir}/3rdparty/freetts1.2/lib", include: ['*.jar'])
    //testCompile fileTree(dir: './3rdparty/freetts1.2/lib', include: ['*.jar'])
    //testCompile fileTree(dir: './3rdparty/jsapi1.0/lib', include: ['*.jar'])
    //testCompile fileTree(dir: "${project(':org.jvoicexml').projectDir}/3rdparty/sphinx4/lib", include: ['*.jar'])
    //testCompile fileTree(dir: './3rdparty/sphinx4/lib', include: ['*.jar'])

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
    
    runtime fileTree(dir: "./3rdparty/talkingjava/lib", include: ['*.dll'])
}

xslt {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/jsapi10-talking-java-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi10-talking-java-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task xsltDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/jsapi10-talking-java-implementation.xml"
    output "${project(':main').buildDir}/config/jsapi10-talking-java-implementation.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
         include 'freetts-jsapi1.0*.jar',
         'jsapi*.jar',
         'cgjsapi.jar'
    }
}

task copyJNI(type: Copy) {
    from("./3rdparty/talkingjava/lib") {
         include '*.dll'
    }
    into "${project(':org.jvoicexml').buildDir}/jni"
}

jar {
    version = JVOICEXML_VERSION

    dependsOn xslt
    dependsOn xsltDistribution
    dependsOn copyReferencedLibraries
    dependsOn copyJNI

    manifest {
        attributes("Implementation-Title": "JVoiceXML JSAPI 1.0 implementation platform",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.implementation.jsapi10'
}

install {
   version = JVOICEXML_VERSION
   group = JVOICEXML_GROUP
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi10-talking-java-implementation.xml")
    delete file("${project(':org.jvoicexml').buildDir}/jni/cgjsapi170.dll")
    delete file("${project(':org.jvoicexml').buildDir}/jni/cgjsapi170_x64.dll")
}
