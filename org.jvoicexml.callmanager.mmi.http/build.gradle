buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'com.github.eerohele.saxon-gradle' version '0.8.0'
    id 'signing'
    id 'maven-publish'
    id 'checkstyle'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = JVOICEXML_GROUP
version = JVOICEXML_VERSION

configurations {
	jvxmlConfiguration
}

def jvxmlConfigurationDir = "${projectDir}/src/main/config"

checkstyle {
    configFile file("${project(':org.jvoicexml').projectDir}/src/templates/jvoicexml-checks.xml")
}

dependencies {
	jvxmlConfiguration fileTree(jvxmlConfigurationDir)

    compile group: 'org.mozilla', name: 'rhino', version: '1.7.10'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.11'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.9'
    
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.20.v20190813'
    
    testCompile group: 'junit', name: 'junit', version: '4.13.2'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
    compile project(':org.jvoicexml.mmi.events')
    compile project(':org.jvoicexml.profile.mmi')
    compile project(':org.jvoicexml.callmanager.mmi')
}

xslt {
    stylesheet "${jvxmlConfigurationDir}/patch-configuration.xsl"
    input "${jvxmlConfigurationDir}/mmi-callmanager-http.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/mmi-callmanager-http.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task xsltDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${jvxmlConfigurationDir}/patch-configuration.xsl"
    input "${jvxmlConfigurationDir}/mmi-callmanager-http.xml"
    output "${project(':org.jvoicexml').copyConfigurationDistribution.destinationDir}/mmi-callmanager-http.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
        include 'rhino*.jar',
        'json-simple*.jar',
        'httpcore*.jar',
        'httpclient*.jar',
        'jetty-server-*.jar',
        'jetty-http-*.jar',
        'jetty-io-*.jar',
        'jetty-util-*.jar',
        'javax.servlet-api-*.jar',
        'org.jvoicexml.client*.jar',
        'org.jvoicexml.mmi.events*.jar',
        'org.jvoicexml.profile.mmi*.jar',
        'org.jvoicexml.profile.vxml21*.jar',
        'org.jvoicexml.callmanager.mmi*.jar'
    }
}

jar {
    version = JVOICEXML_VERSION

    dependsOn xslt
    dependsOn xsltDistribution
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML HTTP ETL of the MMI call manager",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.callmanager.mmi.http'
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/mmi-callmanager-http.xml")
    delete file("${project(':org.jvoicexml').copyConfigurationDistribution.destinationDir}/mmi-callmanager-http.xml")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = tasks.jar.baseName
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'JVoiceXML Socket ETL of the MMI call manager'
                description = 'JVoiceXML Socket ETL of the MMI call manager'
                url = 'https://github.com/JVoiceXML/JVoiceXML'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, Version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
                developers {
                    developer {
                        id = 'schnelle'
                        name = 'Dirk Schnelle-Walka'
                        email = 'dirk.schnelle@jvoicexml.org'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:JVoiceXML/JVoiceXML.git'
                    developerConnection = 'scm:git:ssh://github.com:JVoiceXML/JVoiceXML.git'
                    url = 'https://github.com/JVoiceXML/JVoiceXML'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/" 
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = JVOICEXML_OSSRH_USERNAME
                password = JVOICEXML_OSSRH_PASSWORD
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

