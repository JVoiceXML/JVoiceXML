buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
   }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"
apply plugin: 'maven'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceSets {
    main {
        resources {
            exclude '**'
        }
    }
}

dependencies {
    compile group: 'org.jvoicexml', name: 'javax.speech', version: '0.6-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2', version: '0.6-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2.freetts', version: '0.6-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2.sphinx4', version: '0.6-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'org.jvoicexml.jsapi2.sapi', version: '0.6-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'cmu_us_kal', version: '1.2.3-SNAPSHOT'
    compile group: 'org.jvoicexml', name: 'cmu_time_awb', version: '1.2.3-SNAPSHOT'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
}

task xsltSphinxFreeTTS(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/jsapi20-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi20-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: version,
        libs: configurations.compile
    )
}

task xsltSAPI(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${projectDir}/src/main/resources/patch-configuration.xsl"
    input "${projectDir}/src/main/resources/jsapi20-sapi-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi20-sapi-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: version,
        libs: configurations.compile
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
         include 'freetts*.jar',
         'cmu_us_kal*.jar',
         'cmulex*.jar',
         'en_us*.jar',
         'sphinx4*.jar',
         'javax.speech*.jar',
         'org.jvoicexml.jsapi2*.jar'
    }
}

task copyJNI(type: Copy) {
    dependsOn copyReferencedLibraries
    
    from(zipTree("$buildDir/libs/org.jvoicexml.jsapi2.sapi-0.6-SNAPSHOT.jar")) {
         include '*.dll'
    }
    into "${project(':org.jvoicexml').buildDir}/jni"
}

jar {
    dependsOn xsltSphinxFreeTTS {
        onlyIf { 
            project.property('org.jvoicexml.implementation.jsapi20.freettssphinx') == 'true'
        }
    }
    dependsOn xsltSAPI {
        onlyIf { 
            project.property('org.jvoicexml.implementation.jsapi20.sapi') == 'true'
        }
    }
    dependsOn copyJNI {
        onlyIf { 
            project.property('org.jvoicexml.implementation.jsapi20.sapi') == 'true'
        }
    }
    dependsOn copyReferencedLibraries

    manifest {
        attributes("Implementation-Title": "JVoiceXML JSAPI 2.0 implementation platform",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": version)
    }
    baseName 'org.jvoicexml.implementation.jsapi20'
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi20-implementation.xml")
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/jsapi20-sapi-implementation.xml")
    delete file("${project(':org.jvoicexml').buildDir}/jni/Jsapi2SapiBridge.dll")
    delete file("${project(':org.jvoicexml').buildDir}/jni/Jsapi2SapiBridge_x64.dll")
}
