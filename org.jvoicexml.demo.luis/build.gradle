apply plugin: 'java'
apply plugin: "com.github.eerohele.saxon-gradle"
apply plugin: 'application'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath  "gradle.plugin.com.github.eerohele:saxon-gradle:0.2.1"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    jvxmlConfiguration
}

dependencies {
    jvxmlConfiguration fileTree(dir: "src/main/config")
    
    compile fileTree(dir: rootDir, include: ['*.properties'])
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.9.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.9.1'
    compile group: 'org.jvoicexml', name: 'org.jvoicexml.client', version: JVOICEXML_VERSION
    compile group: 'org.jvoicexml', name: 'org.jvoicexml.client.text', version: JVOICEXML_VERSION
    compile group: 'org.jvoicexml', name: 'org.jvoicexml.xml', version: JVOICEXML_VERSION

    runtime group: 'org.jvoicexml', name: 'org.jvoicexml.jndi', version: JVOICEXML_VERSION
}

xslt {
    stylesheet "${projectDir}/src/main/config/patch-vxml.xsl"
    input "${projectDir}/src/main/config/pizza.vxml"
    output "${sourceSets.main.output.resourcesDir}/pizza.vxml"
    parameters(
        applicationId: project.property('org.jvoicexml.demo.luis.applicationId')
    )
}

jar {
    version = JVOICEXML_VERSION

    dependsOn xslt

    manifest {
        attributes("Implementation-Title": "JVoiceXML LUIS Demo",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": JVOICEXML_VERSION)
    }
    baseName 'org.jvoicexml.demo.luis'
}

mainClassName = "org.jvoicexml.demo.luis.LUISDemo"
applicationDefaultJvmArgs = ["-Djava.security.policy=${projectDir}/src/main/config/jvoicexml.policy" ]

run {
    dependsOn xslt
}

