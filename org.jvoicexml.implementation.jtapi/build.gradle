buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'com.github.eerohele.saxon-gradle' version '0.8.0'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = JVOICEXML_GROUP
version = JVOICEXML_VERSION

configurations {
    jvxmlConfiguration
}

def jvxmlConfigurationDir = "${projectDir}/src/main/config"

checkstyle {
    configFile file("${project(':org.jvoicexml').projectDir}/src/templates/jvoicexml-checks.xml")
}

dependencies {
    jvxmlConfiguration fileTree(jvxmlConfigurationDir)

    compile fileTree(dir: './3rdparty/gjtapi1.10/lib', include: ['*.jar'])
    compile fileTree(dir: './3rdparty/jainsip1.19/lib', include: ['*.jar'])
    compile fileTree(dir: './3rdparty/jtapi1.3/lib', include: ['*.jar'])
    compile fileTree(dir: './3rdparty/mjsip1.6/lib', include: ['*.jar'])
    compile fileTree(dir: './3rdparty/stun4j/lib', include: ['*.jar'])
    
    testCompile group: 'junit', name: 'junit', version: '4.13.2'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    compile project(':org.jvoicexml')
    compile project(':org.jvoicexml.client')
}

task xsltImplementation(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${jvxmlConfigurationDir}/patch-configuration.xsl"
    input "${jvxmlConfigurationDir}/jtapi-implementation.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/jtapi-implementation.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}

task xsltImplementationDistribution(type: com.github.eerohele.SaxonXsltTask) {
    stylesheet "${jvxmlConfigurationDir}/patch-configuration.xsl"
    input "${jvxmlConfigurationDir}/jtapi-implementation.xml"
    output "${project(':org.jvoicexml').copyConfigurationDistribution.destinationDir}/jtapi-implementation.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compile
    )
}


task xsltCallmanager(type: com.github.eerohele.SaxonXsltTask)  {
    stylesheet "${jvxmlConfigurationDir}/patch-callmanager-configuration.xsl"
    input "${jvxmlConfigurationDir}/jtapi-callmanager.xml"
    output "${project(':org.jvoicexml').copyConfiguration.destinationDir}/jtapi-callmanager.xml"
    parameters(
        buildpath: "${buildDir}/libs",
        version: JVOICEXML_VERSION,
        libs: configurations.compile,
        providername: project.property('JVOICEXML_IMPLEMENTATION_JTAPI_SIP_PROVIDERNAME'),
        terminal: project.property('JVOICEXML_IMPLEMENTATION_JTAPI_SIP_TERMINAL'),
        inputType: project.property('JVOICEXML_IMPLEMENTATION_JTAPI_SIP_INPUTTYPE'),
        outputType: project.property('JVOICEXML_IMPLEMENTATION_JTAPI_SIP_OUTPUTTYPE')
    )
}

task xsltCallmanagerDistribution(type: com.github.eerohele.SaxonXsltTask)  {
    stylesheet "${jvxmlConfigurationDir}/patch-callmanager-configuration.xsl"
    input "${jvxmlConfigurationDir}/jtapi-callmanager.xml"
    output "${project(':org.jvoicexml').copyConfigurationDistribution.destinationDir}/jtapi-callmanager.xml"
    parameters(
        buildpath: "lib",
        version: JVOICEXML_VERSION,
        libs: configurations.compile,
        providername: project.property('JVOICEXML_IMPLEMENTATION_JTAPI_SIP_PROVIDERNAME'),
        terminal: project.property('JVOICEXML_IMPLEMENTATION_JTAPI_SIP_TERMINAL'),
        inputType: project.property('JVOICEXML_IMPLEMENTATION_JTAPI_SIP_INPUTTYPE'),
        outputType: project.property('JVOICEXML_IMPLEMENTATION_JTAPI_SIP_OUTPUTTYPE')
    )
}

task copyReferencedLibraries(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compile) {
         include 'gjtapi*.jar',
         'jainsip.jar',
         'jtapi-1.3.1*.jar',
         'Stun4J*.jar',
         'sip.jar',
         'ua.jar'
    }
}

task copyConfiguration(type: Copy) {
    from configurations.jvxmlConfiguration.files {
        include 'mjsip.cfg.txt'
        include 'MjSip.properties'
    }

    filter{ it.replaceAll('@sip.terminal@', "${JVOICEXML_IMPLEMENTATION_JTAPI_SIP_TERMINAL}")}
    filter{ it.replaceAll('@sip.port@', "${JVOICEXML_IMPLEMENTATION_JTAPI_SIP_PORT}")}
    filter{ it.replaceAll('@configDir@', "build/config")}

    into "${project(':org.jvoicexml').copyConfiguration.destinationDir}"
}

task copyConfigurationDistribution(type: Copy) {
    from configurations.jvxmlConfiguration.files {
        include 'mjsip.cfg.txt'
        include 'MjSip.properties'
    }

    filter{ it.replaceAll('@sip.terminal@', "${JVOICEXML_IMPLEMENTATION_JTAPI_SIP_TERMINAL}")}
    filter{ it.replaceAll('@sip.port@', "${JVOICEXML_IMPLEMENTATION_JTAPI_SIP_PORT}")}
    filter{ it.replaceAll('@configDir@', "config")}

    into "${project(':org.jvoicexml').copyConfigurationDistribution.destinationDir}"
}

jar {
    version = JVOICEXML_VERSION

    dependsOn xsltImplementation
    dependsOn xsltImplementationDistribution
    dependsOn xsltCallmanager
    dependsOn xsltCallmanagerDistribution
    dependsOn copyReferencedLibraries
    dependsOn copyConfiguration
    dependsOn copyConfigurationDistribution

    manifest {
        attributes("Implementation-Title": "JVoiceXML JTAPI implementation platform",
                   'Implementation-Vendor': 'switch',
                   'Implementation-Version': JVOICEXML_VERSION,
                   'Built-By'       : System.properties['user.name'],
                   'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                   'Created-By'     : "Gradle ${gradle.gradleVersion}",
                   'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                   'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
    baseName 'org.jvoicexml.implementation.jtapi'
}

clean {
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/jtapi-callmanager.xml")
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/jtapi-implementation.xml")
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/MjSip.properties")
    delete file("${project(':org.jvoicexml').copyConfiguration.destinationDir}/mjsip.cg.txt")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = tasks.jar.baseName
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'JVoiceXML JTAPI implementation platform'
                description = 'JVoiceXML JTAPI implementation platform'
                url = 'https://github.com/JVoiceXML/JVoiceXML'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, Version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
                developers {
                    developer {
                        id = 'schnelle'
                        name = 'Dirk Schnelle-Walka'
                        email = 'dirk.schnelle@jvoicexml.org'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:JVoiceXML/JVoiceXML.git'
                    developerConnection = 'scm:git:ssh://github.com:JVoiceXML/JVoiceXML.git'
                    url = 'https://github.com/JVoiceXML/JVoiceXML'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/" 
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = JVOICEXML_OSSRH_USERNAME
                password = JVOICEXML_OSSRH_PASSWORD
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

